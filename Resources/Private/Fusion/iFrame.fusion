prototype(Jonnitto.iFrame:iFrame) < prototype(Neos.Fusion:Tag) {
    configuration = ${Configuration.setting('Jonnitto.iFrame')}
    height = ${q(node).property('height')}
    unit = ${q(node).property('unit')}
    ratio = ${q(node).property('ratio')}
    src = ${q(node).property('link')}

    @if.render = ${this.src || node.context.inBackend}

    @context {
        configuration = ${this.configuration}
        height = ${this.height ? this.height : this.configuration.defaultHeight}
        unit = ${this.unit}
        ratio = ${this.ratio}
        src = ${this.src}
    }

    attributes {
        class = Neos.Fusion:RawArray {
            nodeType = 'jonnitto-iframe'
            container = ${configuration.container}
        }
        style = Jonnitto.iFrame:Style {
            @if.hasNoContainer = ${configuration.container ? false : true}
        }
    }

    content = Jonnitto.iFrame:Content {
        @process.addContainer = Neos.Fusion:Tag {
            @if.hasContainer = ${configuration.container ? true : false}
            attributes.style = Jonnitto.iFrame:Style
            content = ${value}
        }
    }


    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @position = 'end 999999999'
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
