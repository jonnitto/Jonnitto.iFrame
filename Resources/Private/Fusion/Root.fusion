namespace: Fusion = Neos.Fusion

prototype(Jonnitto.iFrame:iFrame) {
	templatePath = 'resource://Jonnitto.iFrame/Private/Templates/NodeTypes/iFrame.html'

	configuration = ${Configuration.setting('Jonnitto.iFrame')}
	@context {
		configuration = ${this.configuration}
		height = ${q(node).property('height') ? q(node).property('height') : this.configuration.defaultHeight}
		ratio = ${q(node).property('ratio')}
	}

	attributes {
		style = Fusion:RawArray {
			position = 'position:relative;'
			width = ${'width:' + configuration.width + ';'}
			height = Fusion:Case {
				hasRatio {
					condition = ${ratio}
					renderer = ${'padding-top:' + ratio + '%;height:0;'}
				}
				default {
					condition = true
					renderer = ${'height:' + height + q(node).property('unit') + ';'}
				}
			}
		}
	}

	iframe = Fusion:Tag {
		tagName = 'iframe'
		attributes {
			allowtransparency = 'true'
			style = Fusion:RawArray {
				default = ${configuration.defaultStyle}
				position = ${configuration.positionStyle}
			}
			src = ${q(node).property('link')}
			src.@process.convertUris = ConvertUris {
				forceConversion = true
			}
		}
	}
}
