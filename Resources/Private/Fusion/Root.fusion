prototype(Jonnitto.iFrame:iFrame) < prototype(Neos.Fusion:Tag) {
    configuration = ${Configuration.setting('Jonnitto.iFrame')}
    height = ${q(node).property('height')}
    unit = ${q(node).property('unit')}
    ratio = ${q(node).property('ratio')}
    src = ${q(node).property('link')}

    @if.render = ${this.src || node.context.inBackend}

    @context {
        configuration = ${this.configuration}
        height = ${this.height ? this.height : this.configuration.defaultHeight}
        unit = ${this.unit}
        ratio = ${this.ratio}
        src = ${this.src}
    }

    attributes {
        class = Neos.Fusion:RawArray {
            nodeType = 'jonnitto-iframe'
            container = ${configuration.container}
        }
        style = Neos.Fusion:Array {
            position = 'position:relative;'
            width = ${'width:' + configuration.width + ';'}
            height = Neos.Fusion:Case {
                hasRatio {
                    condition = ${ratio}
                    renderer = ${'padding-top:' + ratio + '%;height:0;'}
                }
                default {
                    condition = true
                    renderer = ${'height:' + height + unit + ';'}
                }
            }
        }
    }

    content = Neos.Fusion:Case {
        hasSrc {
            condition = ${src ? true : false}
            renderer = Neos.Fusion:Tag {
                tagName = 'iframe'
                attributes {
                    allowtransparency = 'true'
                    style = Neos.Fusion:Array {
                        default = ${configuration.defaultStyle}
                        position = ${configuration.positionStyle}
                    }
                    src = ${src}
                    src.@process.convertUris = Neos.Neos:ConvertUris {
                        forceConversion = true
                    }
                }
            }
        }
        inBackend {
            condition = ${node.context.inBackend}
            renderer = Carbon.Notification:Tag {
                type = 'alert'
                content = ${Translation.translate('noSrc', null, [], 'NodeTypes/iFrame', 'Jonnitto.iFrame')}
            }
        }
    }


    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @position = 'end 999999999'
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
