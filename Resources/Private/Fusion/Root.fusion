prototype(Jonnitto.iFrame:iFrame) < prototype(Neos.Fusion:Tag) {
    configuration = ${Configuration.setting('Jonnitto.iFrame')}
    height = ${q(node).property('height')}
    unit = ${q(node).property('unit')}
    ratio = ${q(node).property('ratio')}
    src = ${q(node).property('link')}

    @context {
        configuration = ${this.configuration}
        height = ${this.height ? this.height : this.configuration.defaultHeight}
        unit = ${this.unit}
        ratio = ${this.ratio}
        src = ${this.src}
    }

    attributes {
        class = Neos.Fusion:RawArray {
            nodeType = 'jonnitto-iframe'
            container = ${configuration.container}
        }
        style = Neos.Fusion:Array {
            position = 'position:relative;'
            width = ${'width:' + configuration.width + ';'}
            height = Neos.Fusion:Case {
                hasRatio {
                    condition = ${ratio}
                    renderer = ${'padding-top:' + ratio + '%;height:0;'}
                }
                default {
                    condition = true
                    renderer = ${'height:' + height + unit + ';'}
                }
            }
        }
    }

    content = Neos.Fusion:Tag {
        tagName = 'iframe'
        attributes {
            allowtransparency = 'true'
            style = Neos.Fusion:Array {
                default = ${configuration.defaultStyle}
                position = ${configuration.positionStyle}
            }
            src = ${src}
            src.@process.convertUris = ConvertUris {
                forceConversion = true
            }
        }
    }

    @process.contentElementWrapping {
        expression = Neos.Neos:ContentElementWrapping
        @position = 'end 999999999'
    }
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
